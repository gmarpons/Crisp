init_msg :-
        write('Initializing Prolog engine.'), nl.

welcome_msg_and_prolog :-
        write('Welcome to the Crisp interactive interface!'), nl,
        write('Enter Ctrl-D to exit.'), nl,
        % set_prolog_flag(verbose, normal),
        prolog.

runTranslationUnitAnalysis(TUMainFileName) :-
        clangFactsFileName(TUMainFileName, ClangFactsFileName),
        writeAllViolationCandidates(ClangFactsFileName).

clangFactsFileName(TUMainFileName, PrologName) :-
        file_base_name(TUMainFileName, CppName),
        file_name_extension(Base, _, CppName),
        file_name_extension(Base, pl, PrologName).

writeAllViolationCandidates(FileName) :-
        open(FileName, write, Stream),
        forall(violationCandidate(Rule, Culprits),
               portray_clause(Stream, violationCandidate(Rule, Culprits))),
        close(Stream).

%% FIXME: the following code doesn't belongs here

violationCandidate('HICPP 3.4.2', [MethodRepr]) :-
        isA(Method, 'CXXMethodDecl'),
        'ValueDecl::type'(Method, MethodType),
        'QualType::canonicalType'(MethodType, CanonicalMethodType),
        isConstFunctionProtoType(CanonicalMethodType),
        'FunctionType::resultType'(CanonicalMethodType, ResultType),
        'QualType::canonicalType'(ResultType, CanonicalResultType),
        isA(CanonicalResultType, 'PointerType'),
        'QualType::typePtr'(CanonicalResultType, ResultTypePtr),
        'PointerType::pointeeType'(ResultTypePtr, PointeeType),
        'QualType::canonicalType'(PointeeType, CanonicalPointeeType),
        \+ 'QualType::constQualified'(CanonicalPointeeType),

                                % This part should be generated by
                                % CRISP compiler
        mangleName(Method, MethodRepr).
